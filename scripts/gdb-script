# Copyright (C) 2023-2025 Jihuan Tian <jihuan_tian@hotmail.com>
#
# This file is part of the HierBEM library.
#
# HierBEM is free software: you can use it, redistribute it and/or modify it
# under the terms of the GNU Lesser General Public License as published by the
# Free Software Foundation, either version 3 of the License, or (at your option)
# any later version. The full text of the license can be found in the file
# LICENSE at the top level directory of HierBEM.

define print_vector
  set $i = 0
  while $i < $arg1
    print $arg0.operator[]($i)
    set $i = $i + 1
  end
end

define print_hmat_ptr_index_ranges
  printf "[%d,%d), [%d,%d)\n", (*$arg0->row_index_range)[0], (*$arg0->row_index_range)[1], (*$arg0->col_index_range)[0], (*$arg0->col_index_range)[1]
end

define print_hmat_index_ranges
  printf "[%d,%d), [%d,%d)\n", (*$arg0.row_index_range)[0], (*$arg0row_index_range)[1], (*$arg0.col_index_range)[0], (*$arg0.col_index_range)[1]
end

define print_same_level_hmat_ptr_index_ranges
  set $current_hmat = $arg0
  while $current_hmat != 0
    print_hmat_ptr_index_ranges $current_hmat
    set $current_hmat = $current_hmat->next_same_level_hmat_node
  end
end

define print_same_level_same_row_hmat_ptr_index_ranges
  set $initial_hmat = $arg0
  set $current_hmat = $arg0
  while $current_hmat != 0
    if (*$current_hmat->row_index_range)[0] == (*$initial_hmat->row_index_range)[0] && (*$current_hmat->row_index_range)[1] == (*$initial_hmat->row_index_range)[1]
      print_hmat_ptr_index_ranges $current_hmat
    end
    set $current_hmat = $current_hmat->next_same_level_hmat_node
  end
end

define print_same_level_same_column_hmat_ptr_index_ranges
  set $initial_hmat = $arg0
  set $current_hmat = $arg0
  while $current_hmat != 0
    if ((*$current_hmat->col_index_range)[0] == (*$initial_hmat->col_index_range)[0] && (*$current_hmat->col_index_range)[1] == (*$initial_hmat->col_index_range)[1])
      print_hmat_ptr_index_ranges $current_hmat
    end
    set $current_hmat = $current_hmat->next_same_level_hmat_node
  end
end
